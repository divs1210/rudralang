ns!(tree-sort,
  {} =>

  letfn place := [tree, x] =>
    cond(
      map-empty?(tree) =>
        {:val x
         :ltree {}
         :rtree {}}
      lt(x, get(tree, :val)) =>
        update(tree, :ltree, place, x)
      :else =>
        update(tree, :rtree, place, x)
    )

  letfn make-tree := [nums] =>
    loop([tree := {}
          remaining := nums] =>
      cond(
        null?(remaining) =>
          tree
        :else =>
          recur(
            place(tree, first(remaining)),
            rest(remaining)
          )
      )
    )

  letfn walk := [tree] =>
    cond(
      map-empty?(tree) =>
        null
      :else =>
        append(
          walk(get(tree, :ltree)),
          list(get(tree, :val)),
          walk(get(tree, :rtree))
        )
    )

  letfn tree-sort := [nums] =>
    walk(make-tree(nums))

  defn!(main!, {}, args =>
    let strs := rest(args)
    let nums := map(string->number, strs)

    println!(tree-sort(nums))
  )
)